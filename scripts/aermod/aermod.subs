
sub read_profiles {
  my ($filename, $num_factors) = @_;
  my %profiles;
  
  open (my $fh, '<', $filename) or die "Could not open file '$filename' $!";
  while (my $line = <$fh>) {
    chomp $line;
    next unless $line;
    next if $line =~ /^#/;
    
    my @data = split(/,/, $line);
    next unless scalar @data >= ($num_factors + 1);
    next unless looks_like_number($data[1]);
    
    my @factors = @data[1..$num_factors];
    my $average = sum(@factors) / scalar @factors;
    
    unless ($average == 0) {
      @factors = map { $_ / $average } @factors;
    }

    my $prof_id = $data[0];
    $prof_id =~ s/"//g;
    $profiles{$prof_id} = \@factors;
  }
  close $fh;
  
  return %profiles;
}

sub get_factors {
  my ($headers_ref, $data_ref, $monthly_ref, $weekly_ref, $daily_ref) = @_;
  my %headers = %{$headers_ref};
  my @data = @{$data_ref};
  my %monthly = %{$monthly_ref};
  my %weekly = %{$weekly_ref};
  my %daily = %{$daily_ref};

  my $qflag = '';
  my $monthly_prof = $data[$headers{'Monthly Prf'}];
  my $weekly_prof = $data[$headers{'Weekly Prf'}];
  my $monday_prof = $data[$headers{'Mon Diu Prf'}];
  # check if all days use same profile
  if ($monday_prof eq $data[$headers{'Tue Diu Prf'}] &&
      $monday_prof eq $data[$headers{'Wed Diu Prf'}] &&
      $monday_prof eq $data[$headers{'Thu Diu Prf'}] &&
      $monday_prof eq $data[$headers{'Fri Diu Prf'}] &&
      $monday_prof eq $data[$headers{'Sat Diu Prf'}] &&
      $monday_prof eq $data[$headers{'Sun Diu Prf'}]) {
    # check if day-of-week is uniform
    if ($weekly_prof eq '7') {
      # check if hour-of-day is uniform
      if ($monday_prof eq '24') {
        $qflag = 'MONTH';
      # check if month-of-year is uniform
      } elsif ($monthly_prof eq '262') {
        $qflag = 'HROFDY';
      }
    }
    $qflag = 'MHRDOW' unless $qflag;
  } else {
    # check if all weekdays use same profile
    if ($monday_prof eq $data[$headers{'Tue Diu Prf'}] &&
        $monday_prof eq $data[$headers{'Wed Diu Prf'}] &&
        $monday_prof eq $data[$headers{'Thu Diu Prf'}] &&
        $monday_prof eq $data[$headers{'Fri Diu Prf'}]) {
      $qflag = 'MHRDOW';
    } else {
      $qflag = 'MHRDOW7';
    }
  }
  
  my @factors;
  if ($qflag eq 'MONTH') {
    die "Unknown monthly profile code: $monthly_prof" unless exists $monthly{$monthly_prof};
    @factors = @{$monthly{$monthly_prof}};
  } elsif ($qflag eq 'HROFDY') {
    die "Unknown hourly profile code: $monday_prof" unless exists $daily{$monday_prof};
    @factors = @{$daily{$monday_prof}};
  } else {
    die "Unknown monthly profile code: $monthly_prof" unless exists $monthly{$monthly_prof};
    my @monthly_factors = @{$monthly{$monthly_prof}};
    die "Unknown weekly profile code: $weekly_prof" unless exists $weekly{$weekly_prof};
    my @weekly_factors = @{$weekly{$weekly_prof}};
    
    my @days;
    my $index;
    # for MHRDOW, check that all weekdays use the same factor
    if ($qflag eq 'MHRDOW' &&
        $weekly_factors[0] == $weekly_factors[1] &&
        $weekly_factors[0] == $weekly_factors[2] &&
        $weekly_factors[0] == $weekly_factors[3] &&
        $weekly_factors[0] == $weekly_factors[4]) {
      my $i = 4;
      @days = map { [$_, $i++] } qw/Fri Sat Sun/;
    } else {
      $qflag = 'MHRDOW7';
      my $i = 0;
      @days = map { [$_, $i++] } qw/Mon Tue Wed Thu Fri Sat Sun/;
    }

    foreach my $day_ref (@days) {
      my ($day, $index) = @$day_ref;
      my $prof = $data[$headers{"$day Diu Prf"}];

      die "Unknown hourly profile code: $prof" unless exists $daily{$prof};
      my @daily_factors = @{$daily{$prof}};

      foreach my $month_factor (@monthly_factors) {
        push @factors, map { $_ * $weekly_factors[$index] * $month_factor } @daily_factors;
      }
    }
  }
  
  return ($qflag, @factors);
}

sub skip_line() {
  my ($line) = @_;
  
  return 1 unless $line;
  return 1 if $line =~ /^# /;
  
  return 0;
}

sub parse_report_line() {
  my ($line) = @_;
  
  # check for header line
  my $is_header = ($line =~ s/^#//);
  
  my @data;
  foreach my $field (split(/;/, $line)) {
    # remove leading and trailing spaces
    $field =~ s/^\s+//;
    $field =~ s/\s+$//;
    push @data, $field;
  }
  
  return ($is_header, @data);
}

sub parse_header() {
  my ($data_ref, $headers_ref, $pollutants_ref) = @_;
  
  my $col_count = 0;
  my $is_pollutant = 0;
  foreach my $field (@$data_ref) {
    $headers_ref->{$field} = $col_count;
    $col_count++;
    
    if ($is_pollutant) {
      push @$pollutants_ref, $field;
    }
    if ($field eq 'Plt Name') {
      $is_pollutant = 1;
    }
  }
}

1;
